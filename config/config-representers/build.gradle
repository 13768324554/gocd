/*
 * Copyright 2017 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'
  }
}

apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'jacoco'
apply plugin: 'groovy'
apply from: rootProject.file('buildSrc/junit5-support.gradle')

description = "Converts pipeline-config entities to and from JSON"
File generatedSrcDir = file("$projectDir/generated/java")

configurations {
  apt
}

dependencies {
  compile project(':config-api')
  apt group: 'cd.go.jrepresenter', name: 'annotation-processor', version: project.versions.jrepresenter
  compile group: 'cd.go.jrepresenter', name: 'annotation', version: project.versions.jrepresenter

  testCompile localGroovy()
  testCompile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.47'

  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: project.versions.junit5
  testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: project.versions.junit5
  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: project.versions.junit5

  testCompile group: 'org.assertj', name: 'assertj-core', version: project.versions.assertJ
  testCompile project(path: ':config-api', configuration: 'testOutput')
  testCompile group: 'net.javacrumbs.json-unit', name: 'json-unit-fluent', version: project.versions.jsonUnit
}

sourceSets {
  main {
    java {
      srcDirs += generatedSrcDir
    }
  }
}

task cleanGeneratedSrcDir(type: Delete) {
  delete generatedSrcDir
}

clean.dependsOn(cleanGeneratedSrcDir)

compileJava {
  // workaround for https://bugs.openjdk.java.net/browse/JDK-8067747
  // since the `generatedSrcDir` is a `srcDir` for the sourceset (for ease of use in intellij)

  dependsOn cleanGeneratedSrcDir
  options.annotationProcessorGeneratedSourcesDirectory = generatedSrcDir
  options.annotationProcessorPath = configurations.apt
}
